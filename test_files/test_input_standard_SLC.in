# This file describes the suppressed-leak catalyst system
# by DY Zhang and K Sarma

domain 1 : short
domain 2 : short
domain 3 : short
domain 4 : long
domain 5 : short
domain 6 : long
domain 7 : short

strand PS : 3* 2* 1* 5 6
strand OP : 1 2 3 4
strand SP : 5 6
strand BS : 7* 6* 5* 1 2 3
strand Cat : 6 7

complex C1 : 
PS OP
(((.. + ))).

complex C2 :
SP BS
(( + .))...

complex Cat :
Cat
..

complex OP :
OP
....

complex SP :
SP
..

complex I1 :
SP Cat BS
(( + .( + )))...

complex I2 :
SP Cat BS
(. + (( + )))...

complex I3 :
BS Cat
((.... + ))

complex I4 :
BS OP PS Cat
(((... + (((. + )))). + ))

complex I5 :
BS PS Cat
(((((( + )))). + ))

complex I6 :
BS PS Cat
(((((( + ))))) + .)

complex W :
BS PS
.((((( + )))))



